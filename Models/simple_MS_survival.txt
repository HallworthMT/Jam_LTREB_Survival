model{
#SURVIVAL #

#HyperPriors
for(m in 1:2){ # different means for migrants and residents #
for(c in 1:ncovs){ # different covariates #
BetaHyperPrior[m,c]~dnorm(0,tauBetaHP[m,c])
sigmaBetaHP[m,c] ~ dgamma(0.001,0.001)
tauBetaHP[m,c] <- 1/pow(sigmaBetaHP[m,c],2)
} # END 
# Survival 
hyperphi[m] ~ dnorm(0,10)
backphi[m] <- exp(hyperphi[m])/(1+exp(hyperphi[m]))
} # END migstatus


MuError ~ dnorm(0,0.001)
sigmaError ~ dunif(0,100)
tauError <- 1/pow(sigmaError,2)

for(t in 1:n.occasions){
Error[t] ~ dnorm(MuError,tauError)
}

# Priors and constraints
for(sp in 1:nspp){ # species
s.phi[sp] ~ dnorm(hyperphi[migstatus[sp]],1)
sp.phi[sp] <- exp(s.phi[sp])/(1+exp(s.phi[sp]))
  for(s in 1:3){ # sex
    for(h in 1:2){ # habitat
   for(t in 1:n.occasions){ # n.occasions
  mean.phi.t[sp,s,h,t] ~ dnorm(sp.phi[sp],10)T(0,1)
  lphi[sp,s,h,t] <- log(mean.phi.t[sp,s,h,t]/(1-mean.phi.t[sp,s,h,t]))
  } # END n.occasions

p.mean[sp,s,h]~dunif(0,1)  
   } # END habitat
} # END sex

# Very simple beta structure #
for(b in 1:ncovs){
sigmaBetaSpecies[sp,b] ~ dunif(0,100)
tauBetaSpecies[sp,b] <- 1/pow(sigmaBetaSpecies[sp,b],2)

for(s in 1:3){ # Sex
beta[s,sp,b] ~ dnorm(BetaHyperPrior[migstatus[sp],b],tauBetaSpecies[sp,b])
} # END sex
} # END ncovs

} # END sp

for(sp in 1:nspp){
for (i in 1:n[sp]){
     for (t in First[i,sp]:(n.occasions-1)){
          phi[i,t,sp]<-mean.phi.t[sp,sex[i,sp],habitat[i,t,sp],t]
          p[i,t,sp]<-p.mean[sp,sex[i,sp],habitat[i,t,sp]]
         }#t
      }#i

# Likelihood
for (i in 1:n[sp]){
        z[i,First[i,sp],sp]<-1
    for (t in (First[i,sp]+1):n.occasions){
        # state process
        z[i,t,sp]~dbern(mu1[i,t,sp]* z[i,t-1,sp])
		  #mu1[i,t,sp]<-mean.phi.t[sp,sex[i,sp],habitat[i,t,sp],t]
        mu1[i,t,sp]<- (1 / (1+exp(-(lphi[sp,sex[i,sp],habitat[i,t,sp],t]+
                                    beta[sex[i,sp],sp,1]*NDVIstart[t-1]+
                                    beta[sex[i,sp],sp,2]*NDVImar[t-1]+
                                    beta[sex[i,sp],sp,3]*NDVIdelta[t-1]+
                                    beta[sex[i,sp],sp,4]*PPTmar[t-1]+
                                    beta[sex[i,sp],sp,5]*PPTsum[t-1]+
                                    beta[sex[i,sp],sp,6]*PPTstart[t-1]+
                                    Error[t]))))
        # Observation process
        y[i,t,sp]~dbern(mu2[i,t,sp])
        mu2[i,t,sp]<-p[i,t-1,sp]*z[i,t,sp]
      }#t
     }#i
	 }#sp
	
# Derived quantities #	
for(t in 1:n.occasions){
for(s in 1:3){
for(h in 1:2){
res.phi[s,h,t] <- mean(mean.phi.t[residents,s,h,t])
mig.phi[s,h,t] <- mean(mean.phi.t[migrants,s,h,t])
}
}
}
for(sp in 1:nspp){
for(i in 1:n[sp]){
Life[i,sp] <- sum(z[i,First[i,sp]:n.occasions,sp])
}
}
}